#include <WiFi.h>
#include <AsyncTCP.h>
#include <ESPAsyncWebSrv.h>
#include <Wire.h>
#include "RTClib.h"

// --- WiFi ---
const char* ssid = "MAJU 2.4G";
const char* password = "luhanaevaluna";

// --- Pines de cada salida ---
const int pinLuz    = 25;   // Luz
const int pinToma1  = 26;   // Tomacorriente 1 (relé)
const int pinToma2  = 27;   // Tomacorriente 2 (relé)
const int led2      = 2;    // LED extra

// --- Variables de estado ---
bool estadoLuz = false;
bool estadoToma1 = false;
bool estadoToma2 = false;

// --- RTC ---
RTC_DS3231 rtc;
char daysOfTheWeek[7][12] = {"Domingo", "Lunes", "Martes","Miércoles", "Jueves", "Viernes", "Sábado"};
String horaRTC = ""; // aquí guardamos la hora

AsyncWebServer server(80);

void setup() 
{
  // Configurar pines como salidas y apagar todo al inicio (HIGH = apagado para relé)
  pinMode(pinLuz, OUTPUT);   digitalWrite(pinLuz, HIGH);   
  pinMode(pinToma1, OUTPUT); digitalWrite(pinToma1, HIGH); 
  pinMode(pinToma2, OUTPUT); digitalWrite(pinToma2, HIGH); 
  pinMode(led2, OUTPUT);     digitalWrite(led2, LOW);

  Serial.begin(115200);

  // --- Iniciar WiFi ---
  WiFi.begin(ssid, password);
  Serial.print("Conectando");
  while (WiFi.status() != WL_CONNECTED) {Serial.print("."); delay(500);}
  Serial.println("\nConectado!");
  Serial.print("IP: ");Serial.println(WiFi.localIP());

  // --- Iniciar RTC ---
  Wire.begin();
  if (!rtc.begin()) 
  {
    Serial.println("No se encuentra el RTC");
    while (1) delay(10);
  }
  if (rtc.lostPower()) 
  {
    Serial.println("RTC sin hora, ajustar la fecha/hora");
    // rtc.adjust(DateTime(2025, 9, 16, 2, 44, 30)); // ejemplo
  }

  // Página principal
  server.on("/", HTTP_GET, [](AsyncWebServerRequest *request){
    String html = "<!DOCTYPE html><html><head><meta name='viewport' content='width=device-width, initial-scale=1'>";
    html += "<style>body{font-family:Arial;text-align:center;}button{padding:10px 20px;margin:10px;font-size:16px;border:none;border-radius:5px;} .on{background:#4CAF50;color:white;} .off{background:#f44336;color:white;}</style>";
    html += "<script>";
    // script para botones
    html += "function toggle(dev){fetch('/toggle?dev='+dev).then(r=>r.text()).then(t=>{document.getElementById(dev).innerHTML=t;document.getElementById(dev).className=(t.includes('ON')?'on':'off');});}";
    // script para hora cada segundo
    html += "setInterval(()=>{fetch('/hora').then(r=>r.text()).then(t=>{document.getElementById('hora').innerHTML=t;});},1000);";
    html += "</script></head><body>";
    html += "<h1>Panel de Control</h1>";
    html += "<h2>Hora RTC: <span id='hora'>--:--:--</span></h2>";

    if (estadoLuz) {
      html += "<button id='luz' class='on' onclick=\"toggle('luz')\">Luz ON</button><br>";
    } else {
      html += "<button id='luz' class='off' onclick=\"toggle('luz')\">Luz OFF</button><br>";
    }

    if (estadoToma1) {
      html += "<button id='toma1' class='on' onclick=\"toggle('toma1')\">Tomacorriente 1 ON</button><br>";
    } else {
      html += "<button id='toma1' class='off' onclick=\"toggle('toma1')\">Tomacorriente 1 OFF</button><br>";
    }

    if (estadoToma2) {
      html += "<button id='toma2' class='on' onclick=\"toggle('toma2')\">Tomacorriente 2 ON</button><br>";
    } else {
      html += "<button id='toma2' class='off' onclick=\"toggle('toma2')\">Tomacorriente 2 OFF</button><br>";
    }

    html += "</body></html>";
    request->send(200, "text/html", html);
  });

  // Ruta única para cambiar estado
  server.on("/toggle", HTTP_GET, [](AsyncWebServerRequest *request){
    if (request->hasParam("dev")) {
      String dev = request->getParam("dev")->value();
      String respuesta;

      if (dev == "luz") {
        estadoLuz = !estadoLuz; // cambiar estado
        if (estadoLuz) {
          digitalWrite(pinLuz, LOW); // encender
          respuesta = "Luz ON";
        } else {
          digitalWrite(pinLuz, HIGH); // apagar
          respuesta = "Luz OFF";
        }
      } 
      else if (dev == "toma1") {
        estadoToma1 = !estadoToma1;
        if (estadoToma1) {
          digitalWrite(pinToma1, LOW);
          respuesta = "Tomacorriente 1 ON";
        } else {
          digitalWrite(pinToma1, HIGH);
          respuesta = "Tomacorriente 1 OFF";
        }
      } 
      else if (dev == "toma2") {
        estadoToma2 = !estadoToma2;
        if (estadoToma2) {
          digitalWrite(pinToma2, LOW);
          respuesta = "Tomacorriente 2 ON";
        } else {
          digitalWrite(pinToma2, HIGH);
          respuesta = "Tomacorriente 2 OFF";
        }
      } 
      else {
        respuesta = "Dispositivo desconocido";
      }

      request->send(200, "text/plain", respuesta);
    } else {
      request->send(400, "text/plain", "Parámetro dev faltante");
    }
  });

  // Ruta para la hora del RTC
  server.on("/hora", HTTP_GET, [](AsyncWebServerRequest *request){
    request->send(200, "text/plain", horaRTC);
  });

  server.begin();
}

void loop() {
  // leer RTC y actualizar hora
  DateTime now = rtc.now();
  horaRTC = String(now.hour()) + ":" +
            (now.minute()<10?"0":"") + String(now.minute()) + ":" +
            (now.second()<10?"0":"") + String(now.second());

  // --- Condicional LED2 --- (ejemplo: encender primeros 10 segundos)
  if (now.second() < 10) {  
    digitalWrite(led2, HIGH); 
  } else {
    digitalWrite(led2, LOW);  
  }

  delay(1000); // cada segundo
}
